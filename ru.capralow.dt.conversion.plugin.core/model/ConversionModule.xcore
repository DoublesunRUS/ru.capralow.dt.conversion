@GenModel(loadInitialization="false", literalsInterface="true", nonNLSMarkers="true", prefix="cm", updateClasspath="false")
@Ecore(nsPrefix="cm", nsURI="http://ru.capralow.dt.conversion.plugin.core.cm.model")
package ru.capralow.dt.conversion.plugin.core.cm.model

import com._1c.g5.v8.dt.metadata.mdclass.MdObject
import com._1c.g5.v8.dt.metadata.mdclass.Subsystem

class ConversionModule {
	String storeVersion = "1"
	
	contains CmSubsystem[] subsystems
	
	op CmSubsystem getSubsystem(Subsystem mdSubsystem) {
		for (CmSubsystem cmSubsystem : subsystems) {
			if (mdSubsystem.equals(cmSubsystem.subsystem)) return cmSubsystem
		}
		return null;
	}
	
	String[] params
	
	String beforeConvertationEvent = ""
	String
	 beforeConvertationEventPrefix = "Процедура ПередКонвертацией(КомпонентыОбмена) Экспорт"
	String beforeConvertationEventSuffix = "КонецПроцедуры"
	op String getBeforeConvertationEventText() {
		var beforeConvertationEventText = beforeConvertationEvent;
		if (beforeConvertationEvent.isEmpty())
			beforeConvertationEventText = "	";
		
		var result = beforeConvertationEventPrefix + System.lineSeparator()
			+ beforeConvertationEventText + System.lineSeparator()
			+ beforeConvertationEventSuffix + System.lineSeparator();
		return result;
	}
	
	String afterConvertationEvent = ""
	String afterConvertationEventPrefix = "Процедура ПослеКонвертации(КомпонентыОбмена) Экспорт"
	String afterConvertationEventSuffix = "КонецПроцедуры"
	op String getAfterConvertationEventText() {
		var afterConvertationEventText = afterConvertationEvent;
		if (afterConvertationEvent.isEmpty())
			afterConvertationEventText = "	";
		
		var result = afterConvertationEventPrefix + System.lineSeparator()
			+ afterConvertationEventText + System.lineSeparator()
			+ afterConvertationEventSuffix + System.lineSeparator();
		return result;
	}
	
	String beforeFillingEvent = ""
	String beforeFillingEventPrefix = "Процедура ПередОтложеннымЗаполнением(КомпонентыОбмена) Экспорт"
	String beforeFillingEventSuffix = "КонецПроцедуры"
	op String getBeforeFillingEventText() {
		var beforeFillingEventText = beforeFillingEvent;
		if (beforeFillingEvent.isEmpty())
			beforeFillingEventText = "	";
		
		var result = beforeFillingEventPrefix + System.lineSeparator()
			+ beforeFillingEventText + System.lineSeparator()
			+ beforeFillingEventSuffix + System.lineSeparator();
		return result;
	}
	
	contains CmDataRule[] dataRules
	
	op CmDataRule getDataRule(String ruleName) {
		for (CmDataRule dataRule : dataRules) {
			if (ruleName.equals(dataRule.name)) return dataRule
		}
		return null;
	}
	
	op CmDataRule[] getSendingDataRules() {
		val result = newBasicEList()

		for (CmDataRule dataRule : dataRules) {
			if (dataRule.forSending)
				result.add(dataRule)
		}
		return result
	}
	op CmDataRule[] getSendingDataRules(CmSubsystem subsystem) {
		val result = newBasicEList()

		for (CmDataRule dataRule : dataRules) {
			if (dataRule.forSending && dataRule.includedInSubsystem(subsystem))
				result.add(dataRule)
		}
		return result
	}

	op CmDataRule[] getReceivingDataRules() {
		val result = newBasicEList()

		for (CmDataRule dataRule : dataRules) {
			if (dataRule.forReceiving)
				result.add(dataRule)
		}
		return result
	}
	op CmDataRule[] getReceivingDataRules(CmSubsystem subsystem) {
		val result = newBasicEList()

		for (CmDataRule dataRule : dataRules) {
			if (dataRule.forReceiving && dataRule.includedInSubsystem(subsystem))
				result.add(dataRule)
		}
		return result
	}

	contains CmObjectRule[] objectRules
	op CmObjectRule getObjectRule(String ruleName) {
		for (CmObjectRule objectRule : objectRules) {
			if (ruleName.equals(objectRule.name)) return objectRule
		}
		return null;
	}

	op CmObjectRule[] getSendingObjectRules() {
		val result = newBasicEList()

		for (CmObjectRule objectRule : objectRules) {
			if (objectRule.forSending)
				result.add(objectRule)
		}
		return result
	}
	op CmObjectRule[] getSendingObjectRules(CmSubsystem cmSubsystem) {
		val result = newBasicEList()

		for (CmObjectRule objectRule : objectRules) {
			if (objectRule.forSending && objectRule.includedInSubsystem(cmSubsystem))
				result.add(objectRule)
		}
		return result
	}

	op CmObjectRule[] getReceivingObjectRules() {
		val result = newBasicEList()

		for (CmObjectRule objectRule : objectRules) {
			if (objectRule.forReceiving)
				result.add(objectRule)
		}
		return result
	}
	op CmObjectRule[] getReceivingObjectRules(CmSubsystem cmSubsystem) {
		val result = newBasicEList()

		for (CmObjectRule objectRule : objectRules) {
			if (objectRule.forReceiving && objectRule.includedInSubsystem(cmSubsystem))
				result.add(objectRule)
		}
		return result
	}

	contains CmPredefined[] predefineds

	contains CmAlgorithm[] algorithms
	op CmAlgorithm getAlgorithm(String algorithmName) {
		for (CmAlgorithm algorithm : algorithms) {
			if (algorithmName.equals(algorithm.name)) return algorithm
		}
		return null;
	}

	op String getAllAlgorithmsText(String algorithmName) {
		var result = ""

		for (CmAlgorithm algorithm : algorithms) {
			if (algorithmName != algorithm.name && algorithm.exists)
				result += algorithm.getAlgorithmText();

		}

		return result;
	}
	
}

class CmObject {
	String name = ""
	
	refers MdObject configurationObject
	String configurationObjectName = ""
	derived String configurationObjectFormattedName get {
		var result = "";
		
		if (configurationObjectName.startsWith("Метаданные.Справочники")) {
			result = "Справочник." + configurationObjectName.split("[.]").get(2);
		
		} else if (configurationObjectName.startsWith("Метаданные.Документы")) {
			result = "Документ." + configurationObjectName.split("[.]").get(2);
			
		} else if (configurationObjectName.startsWith("Метаданные.Перечисления")) {
			result = "Перечисление." + configurationObjectName.split("[.]").get(2);
			
		} else if (configurationObjectName.startsWith("Метаданные.ПланыВидовХарактеристик")) {
			result = "ПланВидовХарактеристик." + configurationObjectName.split("[.]").get(2);
			
		} else if (configurationObjectName.startsWith("Метаданные.ПланыВидовРасчета")) {
			result = "ПланВидовРасчета." + configurationObjectName.split("[.]").get(2);
			
		} else if (configurationObjectName.startsWith("Метаданные.РегистрыСведений")) {
			result = "РегистрСведений." + configurationObjectName.split("[.]").get(2);
			
		}
		
		return result;
	}

	String formatObject = ""
	
	Boolean forSending = "false"
	Boolean forReceiving = "false"
}

class CmDataRule extends CmObject {
	Boolean isDisabled = "false"
	
	CmSelectionVariant selectionVariant
	
	String onProcessingEvent = ""
	derived String onProcessingEventPrefix get {
		if (forSending) {
			return "Процедура ПОД_&ИмяПОД_ПриОбработке(ДанныеИБ, ИспользованиеПКО, КомпонентыОбмена)"
			.replace("&ИмяПОД", name);
			
		} else
			return "Процедура ПОД_&ИмяПОД_ПриОбработке(ДанныеXDTO, ИспользованиеПКО, КомпонентыОбмена)"
			.replace("&ИмяПОД", name);
	}
	String onProcessingEventSuffix = "КонецПроцедуры"
	op String getOnProcessingEventText() {
		var result = onProcessingEventPrefix + System.lineSeparator()
			+ onProcessingEvent + System.lineSeparator()
			+ onProcessingEventSuffix + System.lineSeparator();
		return result;
	}
	op String getOnProcessingEventDeclaration() {
		if (onProcessingEvent.length == 0)
			return ""
		else
			return "ПОД_&ИмяПОД_ПриОбработке".replace("&ИмяПОД", name);
	}
	
	String dataSelectionEvent = ""
	derived String dataSelectionEventPrefix get {
		return "Функция ПОД_&ИмяПОД_ВыборкаДанных(КомпонентыОбмена)"
			.replace("&ИмяПОД", name);
	}
	String dataSelectionEventSuffix = "КонецФункции"
	op String getDataSelectionEventText() {
		var result = dataSelectionEventPrefix + System.lineSeparator()
			+ dataSelectionEvent + System.lineSeparator()
			+ dataSelectionEventSuffix + System.lineSeparator();
		return result;
	}
	op String getDataSelectionEventDeclaration() {
		if (dataSelectionEvent.length == 0)
			return ""
		else
			return "ПОД_&ИмяПОД_ВыборкаДанных".replace("&ИмяПОД", name);
	}
	
	Boolean isDataCleaning = "false"
	op String getDataCleaningDeclaration() {
		if (isDataCleaning)
			return "Истина"
		else
			return "Ложь";
	}
	
	refers CmObjectRule[] objectRules
	
	refers CmSubsystem[] subsystems
	
	op Boolean includedInSubsystem(CmSubsystem cmSubsystem) {
		if (cmSubsystem === null || cmSubsystem.specialSubsystemType == CmSpecialSubsystemType.ALL)
			return true;
		
		for (CmSubsystem subsystem : subsystems) {
			if (subsystem.equals(cmSubsystem))
				return true;
		}

		for (CmObjectRule objectRule : objectRules) {
			if (objectRule.includedInSubsystem(cmSubsystem))
				return true
		}
		
		if (cmSubsystem.specialSubsystemType != CmSpecialSubsystemType.EMPTY)
			return false;
			
		if (subsystems.size() != 0)
			return false;
			
		for (CmObjectRule objectRule : objectRules) {
			if (objectRule.subsystems.size() != 0)
				return false
		}

		return true;
	}
	
	refers CmGroup group
	
	op String toString() {
		var nameString = name;
		if (name.isEmpty())
			nameString = "<Пустое>";

		var configurationString = configurationObjectName;
		if (configurationObjectName.isEmpty())
			configurationString = "<Пустое>";
		
		var formatString = formatObject;
		if (formatObject.isEmpty())
			formatString = "<Пустое>";
		
		var routeString = "<НаправлениеНеЗадано>";
		if (forSending)
			routeString = "Отправка";
		if (forReceiving)
			routeString = "Получение";
		if (forSending && forReceiving)
			routeString = "ОтправкаИПолучение";
		
		return "name:" + nameString  + " md:" + configurationString + " xdto:" + formatString + " " + routeString;
	}
}

enum CmSelectionVariant {
	Standart as "Стандартная выборка" = 0
	Custom as "Произвольный алгоритм" = 1
}

class CmObjectRule extends CmObject {
	Boolean isDisabled = "false"
	
	Boolean forGroup = "false"
	op String getForGroupDeclaration() {
		if (forGroup)
			return "Истина"
		else
			return "Ложь";
	}
	
	contains CmAttributeRule[] attributeRules

	String onSendingEvent = ""
	derived String onSendingEventPrefix get {
		return "Процедура ПКО_&ИмяПКО_ПриОтправкеДанных(ДанныеИБ, ДанныеXDTO, КомпонентыОбмена, СтекВыгрузки)"
			.replace("&ИмяПКО", name);
	}
	String onSendingEventSuffix = "КонецПроцедуры"
	op String getOnSendingEventText() {
		var result = onSendingEventPrefix + System.lineSeparator()
			+ onSendingEvent + System.lineSeparator()
			+ onSendingEventSuffix + System.lineSeparator();
		return result;
	}
	op String getOnSendingEventDeclaration() {
		if (onSendingEvent.length == 0)
			return ""
		else
			return "ПКО_&ИмяПКО_ПриОтправкеДанных".replace("&ИмяПКО", name);
	}
	
	String beforeReceivingEvent = ""
	derived String beforeReceivingEventPrefix get {
		return "Процедура ПКО_&ИмяПКО_ПриКонвертацииДанныхXDTO(ДанныеXDTO, ПолученныеДанные, КомпонентыОбмена)"
			.replace("&ИмяПКО", name);
	}
	String beforeReceivingEventSuffix = "КонецПроцедуры"
	op String getBeforeReceivingEventText() {
		var result = beforeReceivingEventPrefix + System.lineSeparator()
			+ beforeReceivingEvent + System.lineSeparator()
			+ beforeReceivingEventSuffix + System.lineSeparator();
		return result;
	}
	op String getBeforeReceivingEventDeclaration() {
		if (beforeReceivingEvent.length == 0)
			return ""
		else
			return "ПКО_&ИмяПКО_ПриКонвертацииДанныхXDTO".replace("&ИмяПКО", name);
	}
	
	String onReceivingEvent = ""
	derived String onReceivingEventPrefix get {
		return "Процедура ПКО_&ИмяПКО_ПередЗаписьюПолученныхДанных(ПолученныеДанные, ДанныеИБ, КонвертацияСвойств, КомпонентыОбмена)"
			.replace("&ИмяПКО", name);
	}
	String onReceivingEventSuffix = "КонецПроцедуры"
	op String getOnReceivingEventText() {
		var result = onReceivingEventPrefix + System.lineSeparator()
			+ onReceivingEvent + System.lineSeparator()
			+ onReceivingEventSuffix + System.lineSeparator();
		return result;
	}
	op String getOnReceivingEventDeclaration() {
		if (onReceivingEvent.length == 0)
			return ""
		else
			return "ПКО_&ИмяПКО_ПередЗаписьюПолученныхДанных".replace("&ИмяПКО", name);
	}

	refers CmAlgorithm afterReceivingAlgorithm
	op String getAfterReceivingAlgorithmDeclaration() {
		if (afterReceivingAlgorithm === null || afterReceivingAlgorithm.getAlgorithmText.length == 0)
			return ""
		else
			return afterReceivingAlgorithm.name;
	}

	CmIdentificationVariant identificationVariant
	
	String[] identificationFields
	
	refers CmSubsystem[] subsystems
	
	op Boolean includedInSubsystem(CmSubsystem cmSubsystem) {
		if (cmSubsystem === null || cmSubsystem.specialSubsystemType == CmSpecialSubsystemType.ALL)
			return true;
		
		for (CmSubsystem subsystem : subsystems) {
			if (subsystem.equals(cmSubsystem))
				return true;
		}

		if (cmSubsystem.specialSubsystemType != CmSpecialSubsystemType.EMPTY)
			return false;
			
		if (subsystems.size() != 0)
			return false;
			
		return true;
	}
	
	op String toString() {
		var nameString = name;
		if (name.isEmpty())
			nameString = "<Пустое>";

		var configurationString = configurationObjectName;
		if (configurationObjectName.isEmpty())
			configurationString = "<Пустое>";
		
		var formatString = formatObject;
		if (formatObject.isEmpty())
			formatString = "<Пустое>";
		
		var routeString = "<НаправлениеНеЗадано>";
		if (forSending)
			routeString = "Отправка";
		if (forReceiving)
			routeString = "Получение";
		if (forSending && forReceiving)
			routeString = "ОтправкаИПолучение";
		
		return "name:" + nameString  + " md:" + configurationString + " xdto:" + formatString + " " + routeString;
	}
}

enum CmIdentificationVariant {
	UUID as "По уникальному идентификатору" = 0
	SearchFields as "По полям поиска" = 1
	UUIDThenSearchFields as "Сначала по уникальному идентификатору потом по полям поиска" = 2
}

class CmAttributeRule {
	String configurationTabularSection = ""
	String configurationAttribute = ""
	
	op String getConfigurationAttributeFullName() {
		var result = configurationAttribute;
		if (configurationTabularSection.length != 0)
			result = configurationTabularSection + "." + result;
		return result;
	}

	String formatTabularSection = ""
	String formatAttribute = ""
	op String getFormatAttributeFullName() {
		var result = formatAttribute;
		if (formatTabularSection.length != 0)
			result = formatTabularSection + "." + result;
		return result;
	}
	
	refers CmObjectRule objectRule
	
	Boolean isCustomRule = "false"
	
	op String toString() {
		var customRuleString = "";
		if (isCustomRule)
			customRuleString = " <Алгоритм>";

		var configurationString = getConfigurationAttributeFullName;
		if (getConfigurationAttributeFullName.isEmpty())
			configurationString = "<Пустое>";
		
		var formatString = getFormatAttributeFullName;
		if (getFormatAttributeFullName.isEmpty())
			formatString = "<Пустое>";
		
		return "md:" + configurationString + " xdto:" + formatString  + customRuleString;
	}
}

class CmPredefined extends CmObject {
	contains CmPredefinedMap[] predefinedMaps
	
	op Boolean predefinedMapExists(String configurationValue, String formatValue) {
		for (CmPredefinedMap predefinedMap : predefinedMaps) {
			if (configurationValue.equals(predefinedMap.configurationValue)
				&& formatValue.equals(predefinedMap.formatValue)
			) return true
		}
		return false;
	}
	
	op String toString() {
		var nameString = name;
		if (name.isEmpty())
			nameString = "<Пустое>";

		var configurationString = configurationObjectName;
		if (configurationObjectName.isEmpty())
			configurationString = "<Пустое>";
		
		var formatString = formatObject;
		if (formatObject.isEmpty())
			formatString = "<Пустое>";
		
		var routeString = "<НаправлениеНеЗадано>";
		if (forSending)
			routeString = "Отправка";
		if (forReceiving)
			routeString = "Получение";
		if (forSending && forReceiving)
			routeString = "ОтправкаИПолучение";
		
		return "name:" + nameString  + " md:" + configurationString + " xdto:" + formatString + " " + routeString;
	}
}

class CmPredefinedMap {
	String configurationValue
	op String getConfigurationValueName() {
		if (configurationValue === null) {
			return "";
		}

		var configurationValueName = configurationValue.toString();
		configurationValueName = configurationValueName.substring(configurationValueName.indexOf(".") + 1);
		configurationValueName = configurationValueName.substring(configurationValueName.indexOf(".") + 1);
		return configurationValueName;
	}
	String formatValue
	op String getFormatValueName() {
		if (formatValue === null) {
			return "";
		}

		return formatValue.toString();
	}
}

class CmAlgorithm {
	String name = ""
	
	Boolean exists = "false"
	
	CmMethodType methodType
	String params = ""
	Boolean isExport = "false"
	String body = ""
	derived String prefix get {
		var prefix = "";
		
		if (methodType == CmMethodType.PROCEDURE) {
			prefix = "Процедура";
		}
		else {
			prefix = "Функция";
		}
		
		var export = "";
		if (isExport)
			export = " Экспорт";
		
		return prefix + " " + name + "(" + params + ")" + export;
	}
	derived String suffix get {
		var result = "";
		
		if (methodType == CmMethodType.PROCEDURE) {
			result = "КонецПроцедуры";
		}
		else {
			result = "КонецФункции";
		}
		
		return result;
	}
	op String getAlgorithmText() {
		var result = prefix + System.lineSeparator()
			+ body + System.lineSeparator()
			+ suffix + System.lineSeparator();
		
		return result;
	}
	
	op String toString() {
		return prefix;
	}
}

enum CmMethodType {
	Procedure as "Процедура" = 0
	Function as "Функция" = 1
}

class CmSubsystem {
	refers Subsystem subsystem
	
	derived String name get {
		var name = "";
		
		if (specialSubsystemType == CmSpecialSubsystemType.SUBSYSTEM) {
			name = subsystem.getSynonym().get("ru");
		} else {
			name = specialSubsystemType.getLiteral();
		}
	
		return name;
	}
	
	CmSpecialSubsystemType specialSubsystemType
}

enum CmSpecialSubsystemType {
	Subsystem as "<Подсистема>" = 0
	All as "<Все подсистемы>" = 1
	Main as "Главное" = 2
	Empty as "<Остальное>" = 3
}

class CmGroup {
	String name
}
